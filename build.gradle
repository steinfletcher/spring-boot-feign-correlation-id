group 'com.steinf'
version '0.0.1'

apply plugin: 'java'

sourceCompatibility = 1.8

archivesBaseName = "spring-boot-feign-correlation-id"

buildscript {
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0"
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
}

apply plugin: 'propdeps'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

dependencies {
    provided 'org.springframework.boot:spring-boot-starter-web:1.5.9.RELEASE'
    provided 'io.github.openfeign:feign-core:9.5.1'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: 'x', password: 'x')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: 'x', password: 'x')
            }

            pom.project {
                name 'spring-boot-feign-correlation-id'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'SPring boot feign correlation id'
                url 'https://github.com/steinfletcher/spring-boot-feign-correlation-id'

                scm {
                    connection 'git@github.com:steinfletcher/spring-boot-feign-correlation-id.git'
                    developerConnection 'git@github.com:steinfletcher/spring-boot-feign-correlation-id.git'
                    url 'https://github.com/steinfletcher/spring-boot-feign-correlation-id'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'steinfletcher'
                        name 'Stein Fletcher'
                        email 'steinfletcher@gmail.com'
                    }
                }
            }
        }
    }
}
